plugins {
    id 'fabric-loom'
}

tasks.register('cloneJcef', Exec) {
    commandLine 'git', 'submodule', 'update', '--init', '--recursive', 'java-cef'
}

sourceCompatibility = JavaLanguageVersion.of(java_version as int)
targetCompatibility = JavaLanguageVersion.of(java_version as int)

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://maven.fabricmc.net/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

sourceSets {
    jcef {
        java {
            srcDir "java-cef/java"
            exclude "**/tests/**"
        }
    }

    main {
        compileClasspath += jcef.output
        runtimeClasspath += jcef.output
    }
}

processResources.dependsOn(processJcefResources)

jar {
    from sourceSets.jcef.output.classesDirs
    from sourceSets.jcef.output.resourcesDir
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

idea {
    module {
        excludeDirs += file("java-cef/java/tests")
        inheritOutputDirs true
    }
}